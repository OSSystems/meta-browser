# We have a conf and classes directory, append to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a recipes directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "chromium-browser-layer"
BBFILE_PATTERN_chromium-browser-layer := "^${LAYERDIR}/"
BBFILE_PRIORITY_chromium-browser-layer = "7"

LAYERVERSION_chromium-browser-layer = "1"
CHROMIUM_LEGACY_COMPAT = "scarthgap styhead walnascar"
CHROMIUM_COMPAT = "whinlatter"
LAYERSERIES_COMPAT_chromium-browser-layer = "${CHROMIUM_LEGACY_COMPAT} ${CHROMIUM_COMPAT}"
WHINLATTER_OR_NEWER = "${@bb.utils.contains_any('LAYERSERIES_CORENAMES', d.getVar('CHROMIUM_COMPAT'), '1', '0', d)}"
LAYERDEPENDS_chromium-browser-layer = "${@ '' if d.getVar('WHINLATTER_OR_NEWER') == '1' else 'clang-layer'} core openembedded-layer"

# In whinlatter the UNPACKDIR location has changed, and paths created using
# the ${WORKDIR} variable are not valid anymore. However older versions still
# require these.

WHINLATTER_S_PARENT = "${UNPACKDIR}"
NON_WHINLATTER_S_PARENT = "${WORKDIR}"

CORRECT_S_PARENT = "${@d.getVar('WHINLATTER_S_PARENT') if d.getVar('WHINLATTER_OR_NEWER') == '1' else d.getVar('NON_WHINLATTER_S_PARENT')}"
